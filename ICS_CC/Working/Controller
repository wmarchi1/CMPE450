/*
Creators: UMBC ICS CC 2025-2026
Devices Used: Arduino Uno R4 Wifi, NRF24L01+ (Transceiver) and Joystick Module

Function: Providing inputs for ICS Lead Vehicle. Imputs from Joystick are sent over
2.4Ghz signal to Lead.
*/

/*
//Code for unpacking Binary not used now but could be used in reciever
void unpackJoystickData(uint32_t packed,
                        int &joy1X,
                        int &joy2Y)
{
  joy1X =  packed        & 0x03FF;          // bits 0–9
  joy2Y = (packed >> 10) & 0x03FF;          // bits 10–19
}
*/

#include <SPI.h>
#include <nRF24L01.h>
#include <RF24.h>

// Pins for Transciver
#define CE 6
#define CSN 7

RF24 radio(CE, CSN);
const byte address[6] = "00001"; //byte address that transmitter and receiver will use to communicate
bool transmit_cmd(); //function used to transmit joystick readings, returns 1 on success and 0 on failure

// First Joystick Pins
const int xPin1   = A5;
const int yPin1   = A3;
const int butPin1 = 2;

// Variables to hold Joystick Values
int xVal1, yVal1, butState1;

// Timing Control
const unsigned long READ_INTERVAL_MS = 100; 
unsigned long lastReadTime = 0;

//Variable to hold data to be transmitted
uint32_t packedData;

void setup() {
  Serial.begin(9600);
  radio.begin(); // Initialize the radio
  radio.openWritingPipe(address); // Open a writing pipe to the specific address
  radio.setPALevel(RF24_PA_MIN); // Set the Power Amplifier level (MIN, LOW, HIGH, MAX)
  radio.stopListening(); // Set the module as a transmitter

  pinMode(butPin1, INPUT_PULLUP);
}

void loop() {
  unsigned long now = millis();

  // Read joysticks periodically (non-blocking)
  if (now - lastReadTime >= READ_INTERVAL_MS) {
    lastReadTime = now;

    readJoysticks();

    printJoystickData();

    //packedData can be transmitted as is at this point, no joystick press accounted for yet
    packedData = packJoystickData(xVal1, yVal1);

    bool transmission_success = transmit_cmd();
    if (!transmission_success)
      Serial.println("Transmission Failed");
    else{
      Serial.print(" Packed (HEX): 0x");
      Serial.println(packedData, HEX);

      Serial.print(" Packed (BIN): ");
      Serial.println(packedData, BIN);
    }
  }

  // handleWireless();
}

//Pack integers into first 20 bits of 32 bit binary string
uint32_t packJoystickData(int joy1X, int joy2Y) {
  uint32_t packed = 0;

  packed |= (uint32_t)(joy1X & 0x03FF);          // bits 0–9
  packed |= (uint32_t)(joy2Y & 0x03FF) << 10;    // bits 10–19

  return packed;
}

// Read Inputs
void readJoysticks() {
  // Joystick 1
  xVal1 = analogRead(xPin1);
  yVal1 = analogRead(yPin1);
  butState1 = digitalRead(butPin1);
}

// Output Data
void printJoystickData() {
  Serial.print("J1 [X:");
  Serial.print(xVal1);
  Serial.print(" Y:");
  Serial.print(yVal1);
  Serial.print(" B:");
  Serial.print(butState1);
  Serial.print("]  ");
}

bool transmit_cmd() {
  const uint32_t data = packedData;
  bool success = radio.write(&data, sizeof(data)); // Send the data

  if (success)
    return true;

  return false;

}
